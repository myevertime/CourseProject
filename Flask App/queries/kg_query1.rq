SELECT ?movieLabel ?movie_id
WITH
{
SELECT DISTINCT ?movie ?subject ?movie_id
WHERE {
	{?item wdt:P4947 '$TMDB'.}
	UNION
	{?item wdt:P345 '$IMDB'.}
	
	?item wdt:P921 ?subject. #extract subject from movie


	{?movie wdt:P31/wdt:P279* wd:Q11424;
			wdt:P921 ?subject}
	OPTIONAL {?movie wdt:P4947 ?tmdb_id}
	OPTIONAL {?movie wdt:P345 ?imdb_id}
	BIND(COALESCE(?imdb_id, ?tmdb_id) AS ?movie_id).
    FILTER(!ISBLANK(?movie_id))
	MINUS 
	{
	{?movie wdt:P4947 '$TMDB'.} 
	UNION
	{?movie wdt:P345 '$IMDB'.}
	}
}
}AS %get_movie
WHERE
{
INCLUDE %get_movie
SERVICE wikibase:label {
	bd:serviceParam wikibase:language "en" .}       
}
GROUP BY ?movieLabel ?movie_id
ORDER BY DESC(COUNT(?subject))
LIMIT 10